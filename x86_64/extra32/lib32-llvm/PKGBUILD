pkgname=lib32-llvm
rname=llvm
pkgver=16.0.5
pkgrel=1
arch=('x86_64')
url="https://llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('lib32-libffi' 'lib32-zlib' 'lib32-polly' 'lib32-libxml2'
	'lib32-zstd'
	'llvm' 'zstd')
makedepends=('cmake' 'ninja' 'python3')
options=('!lto')
psource=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($psource/$rname-$pkgver.src.tar.xz
	$psource/cmake-$pkgver.src.tar.xz
	llvm-config.h)

prepare() {
	cd $rname-$pkgver.src
	mkdir -p build

	cd ..
	mv cmake-$pkgver.src cmake
}


build() {
	cd "$srcdir/$rname-$pkgver.src/build"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cmake .. \
	-G Ninja \
	-D LLVM_LIBDIR_SUFFIX=32 \
	-D CMAKE_C_FLAGS:STRING=-m32 \
	-D CMAKE_CXX_FLAGS:STRING=-m32 \
	-D LLVM_TARGET_ARCH:STRING=i686 \
	-D LLVM_HOST_TRIPLE=$CHOST \
	-D LLVM_DEFAULT_TARGETS_TRIPLE="i686-pc-linux-gnu" \
	-D CMAKE_BUILD_TYPE=Release \
	-D CMAKE_INSTALL_PREFIX=/usr \
	-D LLVM_BUILD_LLVM_DYLIB=ON \
	-D LLVM_LINK_LLVM_DYLIB=ON \
	-D LLVM_INSTALL_UTILS=OFF \
	-D LLVM_ENABLE_RTTI=ON \
	-D LLVM_ENABLE_FFI=ON \
	-D LLVM_INCLUDE_BENCHMARKS=OFF \
	-D LLVM_ENABLE_BINDINGS=OFF \
	-D LLVM_INCLUDE_TESTS=OFF \
	-D LLVM_BUILD_TESTS=OFF \
	-D LLVM_BUILD_DOCS=OFF \
	-D LLVM_BINUTILS_INCDIR=/usr/include

	ninja all
}

package() {
	cd "$srcdir/$rname-$pkgver.src/build"

	DESTDIR="$pkgdir" ninja install

	# Fix permissions of static libs
	chmod -x "$pkgdir"/usr/lib32/*.a

	mv "$pkgdir/usr/bin/llvm-config" "$pkgdir/usr/lib32/llvm-config"
	mv "$pkgdir/usr/include/llvm/Config/llvm-config.h" \
		"$pkgdir/usr/lib32/llvm-config-32.h"

	rm -rf "$pkgdir"/usr/{bin,include,share/{doc,man,llvm,opt-viewer}}

	# Header stub is taken from Fedora
	install -d "$pkgdir/usr/include/llvm/Config"
	mv "$pkgdir/usr/lib32/llvm-config-32.h" "$pkgdir/usr/include/llvm/Config/"

	mkdir "$pkgdir"/usr/bin
	mv "$pkgdir/usr/lib32/llvm-config" "$pkgdir/usr/bin/llvm-config32"

	# Symlink LLVMgold.so from /usr/lib/bfd-plugins
	install -d "$pkgdir/usr/lib32/bfd-plugins"
	ln -s ../LLVMgold.so "$pkgdir/usr/lib32/bfd-plugins/LLVMgold.so"
}
